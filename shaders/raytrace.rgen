#version 460
#extension GL_EXT_ray_tracing : enable
#extension GL_EXT_buffer_reference2 : require
#extension GL_EXT_scalar_block_layout : enable
// #extension GL_EXT_nonuniform_qualifier : enable
// #extension GL_EXT_debug_printf : require

#include "types.glsl"
#include "functions.glsl"

layout(binding = 0, set = 0) uniform accelerationStructureEXT topLevelAS;
layout(binding = 1, set = 0, rgba32f) uniform image2D image;
layout(location = 0) rayPayloadEXT HitPayload rayPayload;

layout(scalar, binding = 2, set = 0) readonly uniform CameraData
{
    vec3 origin;
    vec3 orientation;
    mat4 invView;
    mat4 invProj;
}
camera;

//push constants block
layout(scalar, push_constant) uniform RayPushConstants
{
    RayPush rayPush;
} push;

const uint rayFlags = gl_RayFlagsOpaqueEXT;
const float tMin = 0.001;
const float tMax = 10000.;

void main()
{
    const vec2 pixelCenter = vec2(gl_LaunchIDEXT) + vec2(0.5);
    const vec2 inUV = pixelCenter / vec2(gl_LaunchSizeEXT);
    const vec2 d = inUV * 2. - 1.;

    const vec3 origin = camera.invView[3].xyz;
    const vec3 target = (camera.invProj * vec4(d.x, d.y, 1, 1)).xyz;
    const vec3 direction = (camera.invView * vec4(normalize(target.xyz), 0)).xyz;

    rayPayload.depth = 0;
    rayPayload.hitValue = vec3(0.);
    traceRayEXT(topLevelAS, // acceleration structure
        rayFlags, // rayFlags
        0xFF, // cullMask
        0, // sbtRecordOffset
        0, // sbtRecordStride
        0, // missIndex
        origin, // ray origin
        tMin, // ray min range
        direction, // ray direction
        tMax, // ray max range
        0 // payload (location = 0)
    );

    imageStore(image, ivec2(gl_LaunchIDEXT), vec4(rayPayload.hitValue, 1.));
}
